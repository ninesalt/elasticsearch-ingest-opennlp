import java.nio.file.Files

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:${elasticsearchVersion}"
  }
}

plugins {
  id "de.undercouch.download" version "3.4.2"
  id "co.riiid.gradle" version "0.4.2"
}

group = 'de.spinscale.elasticsearch.plugin.ingest'
version = "${elasticsearchVersion}.1-SNAPSHOT"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'elasticsearch.esplugin'

// license of this project
licenseFile = rootProject.file('LICENSE.txt')
// copyright notices
noticeFile = rootProject.file('NOTICE.txt')

// disable uploadArchives task for now, no upload happening currently
uploadArchives.enabled = false

esplugin {
  // license of the plugin, may be different than the above license
  licenseFile rootProject.file('LICENSE.txt')
  // copyright notices, may be different than the above notice
  noticeFile rootProject.file('NOTICE.txt')
  name 'ingest-opennlp'
  description 'Ingest processor that uses OpenNLP for NER and POS tagging'
  classname 'de.spinscale.elasticsearch.ingest.opennlp.IngestOpenNlpPlugin'
}


// In this section you declare the dependencies for your production and test code
// Elasticsearch dependency is included due to the build-tools, test-framework as well
dependencies {
  compile 'org.apache.opennlp:opennlp-tools:1.9.1'
}



// dependency license check needs can be enabled
dependencyLicenses.enabled = false

// thirdparty audit needs can be enabled
thirdPartyAudit.enabled = false

// license header checks can be disabled
licenseHeaders.enabled = true

// exlude the models for forbidden API check
forbiddenPatterns {
  exclude '**/*.bin'
}

test {
  exclude '**/*'
}